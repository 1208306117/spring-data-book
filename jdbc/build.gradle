apply from: '../build.gradle'
apply plugin: "java"

[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:all"]

project.sourceCompatibility = 1.6
project.targetCompatibility = 1.6

archivesBaseName = "spring-data-book-jdbc"

eclipse {
  project.natures += "org.springframework.ide.eclipse.core.springnature"
  project.buildCommand("org.springframework.ide.eclipse.core.springbuilder")
}

compileJava {
    source = ['src/main/java', 'src/generated/java']
}

configurations {
  codegen
}

dependencies {

  codegen "com.mysema.querydsl:querydsl-sql-codegen:2.6.0"
  codegen "org.hsqldb:hsqldb:2.2.8"
  codegen "org.slf4j:slf4j-simple:$slf4jVersion"

  compile "com.mysema.querydsl:querydsl-sql:2.6.0"

  // Logging
  compile "org.slf4j:slf4j-api:$slf4jVersion"

  // Spring
  compile("org.springframework:spring-beans:$springVersion") { force = true }
  compile("org.springframework:spring-context:$springVersion") { force = true }
  compile("org.springframework:spring-core:$springVersion") { force = true }
  compile("org.springframework:spring-jdbc:$springVersion") { force = true }
  compile("org.springframework:spring-jms:$springVersion") { force = true }
  compile("org.springframework:spring-oxm:$springVersion") { force = true }

  // Spring Data JDBC-Extensions
  compile "org.springframework.data:spring-data-oracle:1.0.0.RC2"

  // Database
  compile("org.hsqldb:hsqldb:2.2.8")

  // Runtime	
  runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
  runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
  runtime "log4j:log4j:$log4jVersion"

  // Testing
  testCompile("org.springframework:spring-test:$springVersion") { force = true }
  testCompile "junit:junit:4.10"
  testCompile "org.hamcrest:hamcrest-library:1.2.1"

  // proprietary jars
  compile fileTree(dir: 'lib', include: '*.jar')

}

task codegen << {
  ant.taskdef(name: 'codegen',
    classname: 'com.mysema.query.sql.ant.AntMetaDataExporter',
    classpath: configurations.codegen.asPath)
  ant.codegen(jdbcDriverClass: 'org.hsqldb.jdbc.JDBCDriver',
    dbUrl: 'jdbc:hsqldb:hsql://localhost:9001/test',
    dbUserName: 'sa', schemaPattern: 'PUBLIC',
    targetSourceFolder: 'src/generated/java',
    targetPackage: 'com.oreilly.springdata.jdbc.querydsl')
}

task runHelloOracle(type: JavaExec, dependsOn: classes) {
  main = 'com.oreilly.springdata.jdbc.oracle.HelloOracle'
  classpath sourceSets.main.output.classesDir
  classpath sourceSets.main.output.resourcesDir
  classpath configurations.runtime 
}

task runHelloQueryDSL(type: JavaExec, dependsOn: classes) {
  main = 'com.oreilly.springdata.jdbc.querydsl.HelloQueryDsl'
  classpath sourceSets.main.output.classesDir
  classpath sourceSets.main.output.resourcesDir
  classpath configurations.runtime
}
