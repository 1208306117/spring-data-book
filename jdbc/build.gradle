// Dependency versions
ext {
	sdJdbcVersion = "1.0.0.RC3"
	querydslVersion = "2.7.0"
}

// Add the folder with generated classes to source folders
compileJava {
    source = ['src/main/java', 'src/generated/java']
}

// Add a dedicated configuration for code generation
configurations {
  codegen
}

// Execute Querydsl Ant task for code generation
task codegen << {
  
	ant.taskdef(name: 'codegen',
    classname: 'com.mysema.query.sql.ant.AntMetaDataExporter',
    classpath: configurations.codegen.asPath)
	
  ant.codegen(jdbcDriverClass: 'org.hsqldb.jdbc.JDBCDriver',
    dbUrl: 'jdbc:hsqldb:hsql://localhost:9001/test',
    dbUserName: 'sa', 
		schemaPattern: 'PUBLIC',
    targetSourceFolder: 'src/generated/java',
    targetPackage: 'com.oreilly.springdata.jdbc.domain')
}

dependencies {

  codegen "com.mysema.querydsl:querydsl-sql-codegen:$querydslVersion"
  codegen "org.hsqldb:hsqldb:2.2.8"

  compile "com.mysema.querydsl:querydsl-sql:$querydslVersion"
  compile "org.springframework.data:spring-data-jdbc-core:$sdJdbcVersion"
  
  compile("org.springframework:spring-tx:$springVersion") { force = true }
  
  compile "commons-dbcp:commons-dbcp:1.4"
  compile("org.hsqldb:hsqldb:2.2.8")
}
