apply plugin: "base"

// Dependency versions
ext {

	// Logging
	slf4jVersion = "1.6.6"
	logbackVersion = "1.0.6"

	// Core libraries
	springVersion = "3.1.2.RELEASE"
	sdCommonsVersion = "1.3.1.RELEASE"
	junitVersion = "4.10"
	hamcrestVersion = "1.2.1"
	cglibVersion = "2.2.2"

	groovyVersion = "1.8.6"
	version = "1.0.0.BUILD-SNAPSHOT"
}

allprojects {

  apply plugin: "idea"
  apply plugin: "maven"
  apply plugin: "eclipse"

  group = "com.oreilly.springdata"
  description = "Spring Data Book Samples"

  configurations.all {
    exclude group: "commons-logging"
  }

  repositories {
    mavenCentral()
    maven { url "http://repo.springsource.org/libs-snapshot" }
  }

  defaultTasks 'build'
}

configure(subprojects) { subproject ->

  apply plugin: "java"

  [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:all"]

  project.sourceCompatibility = 1.6
  project.targetCompatibility = 1.6

  archivesBaseName = "spring-data-book-${subproject.name}"

  dependencies {

    // Logging
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    runtime "ch.qos.logback:logback-classic:$logbackVersion"

    // Spring
    compile("org.springframework:spring-context:$springVersion") { force = true }
    compile("org.springframework:spring-beans:$springVersion") { force = true }
    compile("org.springframework:spring-expression:$springVersion") { force = true }
    compile("org.springframework:spring-core:$springVersion") { force = true }
    compile "org.springframework.data:spring-data-commons-core:$sdCommonsVersion"
    compile "cglib:cglib:$cglibVersion"

    // Testing
    testCompile("org.springframework:spring-test:$springVersion") { force = true }
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    testCompile "junit:junit-dep:${junitVersion}"
  }

  eclipse {
    project.natures += "org.springframework.ide.eclipse.core.springnature"
    project.buildCommand("org.springframework.ide.eclipse.core.springbuilder")
  }
}

task wrapper(type: Wrapper) { gradleVersion = "1.0" }